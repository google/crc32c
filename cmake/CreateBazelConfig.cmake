# Copyright 2018 The CRC32C Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file. See the AUTHORS file for names of contributors.

# Generate a Bazel configuration file with the headers and sources for a given
# target. The generated file can be loaded from a BUILD file to create the
# corresponding targets in Bazel.
function (create_bazel_config TARGET)
    if (NOT TARGET ${TARGET})
        message(
            FATAL_ERROR "create_bazel_config requires a target name: ${TARGET}")
    endif ()
    set(filename "${TARGET}.bzl")
    set(H)
    set(CC)
    get_target_property(target_type ${TARGET} TYPE)
    if (${target_type} STREQUAL "INTERFACE_LIBRARY")
        get_target_property(sources ${TARGET} INTERFACE_SOURCES)
    else()
        get_target_property(sources ${TARGET} SOURCES)
    endif ()
    foreach (src ${sources})
        string(REPLACE "${PROJECT_BINARY_DIR}/" "$" src "${src}")
        string(REPLACE "${PROJECT_SOURCE_DIR}/" "" src "${src}")
        string(FIND "${src}" "$" has_expression)
          if (NOT "${has_expression}" EQUAL -1)
            # Skip sources that have expressions in them.
        elseif("${src}" MATCHES "\.inc$")
            list(APPEND H ${src})
        elseif("${src}" MATCHES "\.h$")
            list(APPEND H ${src})
        elseif("${src}" MATCHES "\.cc$")
            list(APPEND CC ${src})
        endif ()
    endforeach ()
    file(
        WRITE
            "${filename}"
            "# DO NOT EDIT -- GENERATED BY CMake -- Change the CMakeLists.txt file if needed\n"
        )
    file(APPEND "${filename}" "${TARGET}_HDRS = [\n")
    foreach (src ${H})
        file(APPEND "${filename}" "    \"${src}\",\n")
    endforeach ()
    file(APPEND "${filename}" "]\n\n")
    file(APPEND "${filename}" "${TARGET}_SRCS = [\n")
    foreach (src ${CC})
        file(APPEND "${filename}" "    \"${src}\",\n")
    endforeach ()
    file(APPEND "${filename}" "]\n")
endfunction ()

# Export a list to a .bzl file, mostly used to export names of unit tests.
function (export_list_to_bazel filename VAR)
    file(
        WRITE
            "${filename}"
            "# DO NOT EDIT -- GENERATED BY CMake -- Change the CMakeLists.txt file if needed\n"
        )
    file(APPEND "${filename}" "${VAR} = [\n")
    foreach (item ${${VAR}})
        string(REPLACE "${PROJECT_SOURCE_DIR}/" "" item "${item}")
        file(APPEND "${filename}" "    \"${item}\",\n")
    endforeach ()
    file(APPEND "${filename}" "]\n")
endfunction ()
